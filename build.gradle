import java.time.LocalDateTime
import java.time.ZoneId
import java.time.ZoneOffset

plugins {
    id "org.springframework.boot" version "1.5.8.RELEASE"
}

repositories {
    mavenCentral()
}

dependencies {
    ext.springCloudConnectorVersion = "2.0.1.RELEASE"
    ext.commonsLang3Version = "3.7"

    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework.cloud:spring-cloud-spring-service-connector:$springCloudConnectorVersion"
    compile "org.springframework.cloud:spring-cloud-cloudfoundry-connector:$springCloudConnectorVersion"
    compile "org.apache.commons:commons-lang3:$commonsLang3Version"
    compile "org.postgresql:postgresql"
    compile "org.flywaydb:flyway-core"

    testCompile "org.springframework.boot:spring-boot-starter-test"
}

version "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8
targetCompatibility = 1.8

// *** LifeCycle Tasks *** //

task jsTest(type: Exec) {
    group = "Parrit"
    description = "Runs the frontend javascript tests"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/jest/bin/jest.js"
    args = ["-c", ".jestrc.json"]
}
test.dependsOn(jsTest)

task jsBuild(type: Exec) {
    group = "Parrit"
    description = "Bundles the frontend javascript code"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/grunt-cli/bin/grunt"
    args = ["build"]
}
jsBuild.dependsOn(jsBuild)

defaultTasks "clean", "build"

// *** Helper Tasks *** //

task createMigration(type: Exec) {
    group = "Parrit"
    description = "Creates an empty migration file with a valid version number"

    def utc = ZoneId.of("UTC")
    def timestamp = LocalDateTime.now(utc).toEpochSecond(ZoneOffset.UTC)

    workingDir = "$project.projectDir/src/main/resources/db/migration"
    commandLine = "touch"
    args = ["V${timestamp}__CHANGEME.sql"]
}
